plugins {
    id 'java-library'
    id 'application'
    id 'io.qameta.allure' version '2.8.1'
}

repositories {
    maven {url = "https://dl.bintray.com/qameta/maven-unstable/"}
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

compileJava {
    sourceCompatibility = "1.8"
    targetCompatibility = '1.8'
}

def allureVersion = "2.13.8",
    selenideVersion = "5.18.0",
    junitVersion = "5.7.0",
    javafackerVersion = "1.0.2",
    aspectjweaverVersion = "1.9.6",
    ownerVersion = "1.0.12",
    allureTestopsVersion = "3.28.2",
    restassuredVersion = "4.3.1",
    jacksonVersion = "2.12.2"

allure {
    version = "${allureVersion}"
    autoconfigure = true
    aspectjweaver = true

    useJUnit5 {
        version = "${allureVersion}"
    }

}

dependencies {
    testImplementation (
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "com.github.javafaker:javafaker:$javafackerVersion",
            "org.aspectj:aspectjweaver:$aspectjweaverVersion",
            "org.junit.jupiter:junit-jupiter-api:$junitVersion",
            "org.junit.jupiter:junit-jupiter-params:$junitVersion",
            "io.qameta.allure:allure-ee-junit-platform:$allureTestopsVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "io.rest-assured:rest-assured:${restassuredVersion}",
            "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}",
            "io.qameta.allure:allure-rest-assured:${allureVersion}"
    )
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

tasks.withType(Test) {
    systemProperties += System.properties
    System.setProperty("chromeoptions.prefs", "intl.accept_languages=en")
    System.setProperty("junit.jupiter.extensions.autodetection.enabled", "true")

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }
}

test {
    useJUnitPlatform()
    systemProperties(System.getProperties())
}

task web(type: Test) {
    useJUnitPlatform {
        includeTags 'web'
    }
    systemProperty 'platform', 'web'
}